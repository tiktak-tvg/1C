Процедура ОбработкаПроведения(Отказ, Режим)
	Движения.Проводки.БлокироватьДляИзменения = Истина;
	
	Для каждого ТекСтрока Из Товары Цикл
		Движение = Движения.Проводки.Добавить();
		Движение.СчетДт = ПланыСчетов.Бухгалтерский.Капитал;
		Движение.СчетКт = ПланыСчетов.Бухгалтерский.Товары;
		ОбщийСерверВызов.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Номенклатура", ТекСтрока.Номенклатура);
		ОбщийСерверВызов.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Склады", Склад);
		Движение.Период = Дата;
		Движение.Сумма = 0; // или плановая
		Движение.КоличествоКт = ТекСтрока.Количество;
		Движение.Содержание = "Себестоимость";
		Движение.Организация = Организация;
		ОбщийСерверВызов.УстановитьПодразделенияПроводки(Движение, Подразделение, Подразделение);

		Движение = Движения.Проводки.Добавить();
		Движение.СчетДт = ПланыСчетов.Бухгалтерский.Покупатели;
		ОбщийСерверВызов.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "Контрагенты", Контрагент);
		ОбщийСерверВызов.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "Договоры", Договор);
		Движение.СчетКт = ПланыСчетов.Бухгалтерский.Капитал;
		Движение.Период = Дата;
		Движение.Сумма = ТекСтрока.Сумма;
		Движение.Содержание = "Продажа товаров";
		Движение.Организация = Организация;
		ОбщийСерверВызов.УстановитьПодразделенияПроводки(Движение, Подразделение, Подразделение);
	КонецЦикла;
	
	Движения.Проводки.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПродажаТоваровТовары.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	Документ.ПродажаТоваров.Товары КАК ПродажаТоваровТовары
		|ГДЕ
		|	ПродажаТоваровТовары.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПроводкиОстатки.Субконто1
		|ИЗ
		|	РегистрБухгалтерии.Проводки.Остатки(
		|			&Период,
		|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Бухгалтерский.Товары),
		|			&ВидыСубконто,
		|			Организация = &Организация
		|				И Подразделение = &Подразделение
		|				И Субконто2 = &Склад
		|				И Субконто1 В
		|					(ВЫБРАТЬ
		|						Товары.Номенклатура
		|					ИЗ
		|						Товары)) КАК ПроводкиОстатки
		|ГДЕ
		|	ПроводкиОстатки.КоличествоОстаток < 0";
	Запрос.УстановитьПараметр("Период", Неопределено);
	ПВХ = ПланыВидовХарактеристик.ВидыСубконто;
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПВХ.Номенклатура);
	Если Константы.УчетПоСкладам.Получить() Тогда
		ВидыСубконто.Добавить(ПВХ.Склады);
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Отбор = Новый Структура("Номенклатура", Выборка.Субконто1);
		мСтрок = Товары.НайтиСтроки(Отбор);

		Отказ = Истина;
		
		Для Каждого Стр Из мСтрок Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватило товара " + Строка(Выборка.Субконто1);
			Сообщение.Поле = "Товары[" + (Стр.НомерСтроки - 1) + "].Количество";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры