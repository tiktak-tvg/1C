Процедура УстановитьСубконто(Счет, Субконто, ИмяСубконто, ЗначениеСубконто) Экспорт
	Если НЕ ЗначениеЗаполнено(Счет) ИЛИ НЕ ЗначениеЗаполнено(ИмяСубконто) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСчета = ПолучитьСвойстваСчета(Счет);
	
	Если ТипЗнч(ИмяСубконто) = Тип("Число") Тогда
		Если ИмяСубконто > ДанныеСчета.КоличествоСубконто Тогда
			Возврат;
		КонецЕсли;
		
		ВидСубконтоСсылка = ДанныеСчета["ВидСубконто" + ИмяСубконто];
	Иначе
		ВидСубконтоСсылка = ПланыВидовХарактеристик.ВидыСубконто[ИмяСубконто];
		Если ДанныеСчета.ВидСубконто1 <> ВидСубконтоСсылка
			И ДанныеСчета.ВидСубконто2 <> ВидСубконтоСсылка Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВидСубконтоСсылка.ТипЗначения.СодержитТип(ТипЗнч(ЗначениеСубконто)) Тогда
		Субконто.Вставить(ВидСубконтоСсылка, ЗначениеСубконто);
	КонецЕсли; 
КонецПроцедуры

Функция ПолучитьСвойстваСчета(Счет) Экспорт
	ДанныеСчета = Новый Структура;
	ДанныеСчета.Вставить("Забалансовый", Счет.Забалансовый);
	ДанныеСчета.Вставить("УчетПоПодразделениям", Счет.УчетПоПодразделениям);
	ДанныеСчета.Вставить("КоличествоСубконто", Счет.ВидыСубконто.Количество());
	
	МаксКоличествоСубконто = МетаДанные.ПланыСчетов.Бухгалтерский.МаксКоличествоСубконто;
	
	Для Индекс = 1 По МаксКоличествоСубконто Цикл
		Если Индекс <= Счет.ВидыСубконто.Количество() Тогда
			ДанныеСчета.Вставить("ВидСубконто" + Индекс, Счет.ВидыСубконто[Индекс - 1].ВидСубконто);
			ДанныеСчета.Вставить("ВидСубконто" + Индекс + "Наименование", Строка(Счет.ВидыСубконто[Индекс - 1].ВидСубконто));
			ДанныеСчета.Вставить("ВидСубконто" + Индекс + "ТипЗначения", Счет.ВидыСубконто[Индекс - 1].ВидСубконто.ТипЗначения);
		Иначе
			ДанныеСчета.Вставить("ВидСубконто" + Индекс, Неопределено);
			ДанныеСчета.Вставить("ВидСубконто" + Индекс + "Наименование", Неопределено);
			ДанныеСчета.Вставить("ВидСубконто" + Индекс + "ТипЗначения", Неопределено);
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ДанныеСчета;
КонецФункции