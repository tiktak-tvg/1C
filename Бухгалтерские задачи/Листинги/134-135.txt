Процедура ОбработкаПроведения(Отказ, Режим)
	Движения.Проводки.Записать();
	
	Запрос = Новый Запрос;
	ВидыСубконто = Новый Массив;
	ПВХ = ПланыВидовХарактеристик.ВидыСубконто;
	ВидыСубконто.Добавить(ПВХ.Номенклатура);
	ВидыСубконто.Добавить(ПВХ.Склады);
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	МассивТоваров = Товары.ВыгрузитьКолонку("Номенклатура");
	Запрос.УстановитьПараметр("МассивТоваров", МассивТоваров);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", МоментВремени());
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПродажаТоваровТовары.НомерСтроки,
		|	ПродажаТоваровТовары.Номенклатура,
		|	ПродажаТоваровТовары.Количество,
		|	ПродажаТоваровТовары.Сумма,
		|	ЕСТЬNULL(ПроводкиОстатки.СуммаОстатокДт, 0) КАК ОстСум,
		|	ЕСТЬNULL(ПроводкиОстатки.КоличествоОстатокДт, 0) КАК ОстКол
		|ИЗ
		|	Документ.ПродажаТоваров.Товары КАК ПродажаТоваровТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Проводки.Остатки(
		|				&Период,
		|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Бухгалтерский.Товары),
		|				&ВидыСубконто,
		|				Организация = &Организация
		|					И Подразделение = &Подразделение
		|					И Субконто2 = &Склад
		|					И Субконто1 В (&МассивТоваров)) КАК ПроводкиОстатки
		|		ПО ПродажаТоваровТовары.Номенклатура = ПроводкиОстатки.Субконто1
		|ГДЕ
		|	ПродажаТоваровТовары.Ссылка = &Ссылка";
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Движения.Проводки.Записывать = Истина;
	
	Пока Выборка.Следующий() Цикл
		Стоимость = 0;
		
		Если Выборка.ОстКол < Выборка.Количество Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает " + Выборка.Номенклатура;
			Сообщение.Поле = "Товары[" + (Выборка.НомерСтроки - 1) + "].Количество";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			
			Отказ = Истина;
		ИначеЕсли Выборка.ОстКол = Выборка.Количество Тогда
			Стоимость = Выборка.ОстСум;
		Иначе
			Стоимость = Окр(Выборка.ОстСум * Выборка.Количество / Выборка.ОстКол, 2);
		КонецЕсли;
		
		Движение = Движения.Проводки.Добавить();
		Движение.СчетДт = ПланыСчетов.Бухгалтерский.Капитал;
		Движение.Период = Дата;
		Движение.СчетКт = ПланыСчетов.Бухгалтерский.Товары;
		ОбщийСерверВызов.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Номенклатура", Выборка.Номенклатура);
		ОбщийСерверВызов.УстановитьСубконто(Движение.СчетКт, Движение.СубконтоКт, "Склады", Склад);
		Движение.Сумма = Стоимость;
		Движение.КоличествоКт = Выборка.Количество;
		Движение.Содержание = "Себестоимость";
		Движение.Организация = Организация;
		ОбщийСерверВызов.УстановитьПодразделенияПроводки(Движение, Подразделение, Подразделение);
		
		Движение = Движения.Проводки.Добавить();
		Движение.СчетДт = ПланыСчетов.Бухгалтерский.Покупатели;
		ОбщийСерверВызов.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "Контрагенты", Контрагент);
		ОбщийСерверВызов.УстановитьСубконто(Движение.СчетДт, Движение.СубконтоДт, "Договоры", Договор);
		Движение.СчетКт = ПланыСчетов.Бухгалтерский.Капитал;
		Движение.Период = Дата;
		Движение.Сумма = Выборка.Сумма;
		Движение.Содержание = "Продажа товаров";
		Движение.Организация = Организация;
		ОбщийСерверВызов.УстановитьПодразделенияПроводки(Движение, Подразделение, Подразделение);
	КонецЦикла;
	
	Для Каждого ТекСтрокаУслуги Из Услуги Цикл
		Движение = Движения.Проводки.Добавить();
		Движение.СчетДт = ПланыСчетов.Бухгалтерский.Покупатели;
		ОбщийСерверВызов.УстановитьСубконто(Движение.СчетДт,
			Движение.СубконтоДт, "Контрагенты", Контрагент);
		ОбщийСерверВызов.УстановитьСубконто(Движение.СчетДт,
			Движение.СубконтоДт, "Договоры", Договор);
		Движение.СчетКт = ПланыСчетов.Бухгалтерский.Капитал;
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Сумма = ТекСтрокаУслуги.Сумма;
		Движение.Содержание = "Выручка";
		ОбщийСерверВызов.УстановитьПодразделенияПроводки(Движение, Подразделение, Подразделение);
	КонецЦикла;
КонецПроцедуры